#27200 1
Игрок берет яйцо из гнезда совы~
1 g 100
~

  Описание триггеров в этой зоне содержит много подробностей (для билдеров оно).
  Вам конечно не надо описывать каждое ваше действие, и каждую команду,
  но хотя-бы ключевые моменты опишите обязательно.

  Некоторые триггеры были модифицированны либо добавлены при создании второй версии
  этой зоны. Поэтому их порядок произволен.
   
  !!! ВНИМАНИЕ !!!
  В этой зоне все так наворочено СПЕЦИАЛЬНО :)
  Это ПРИМЕР не того, как делать зоны, а того какие триггеры писать. 

  Будьте осторожны и не делайте по стольку триггеров в своей зоне, если вы не являетеся
  бывалым программистом. Для нормальной зоны и 20% хватило бы Ж)

  Особенно не рекомендую заниматься беспорядочными проверками и улучшениями
  имеющими лишь косметический характер и срабатывающими раз в год.
  (см. триггер барона, реакция на отравленную бутылку например явно излишня)

  !!! ВНИМАНИЕ !!!
  И тут могут быть ошибки и недоработки. Они есть везде.
  Однако сами понимаете вероятность их невелика. 
  !!! ВНИМАНИЕ !!!
  Как показала практика в этой зоне содержалось как минимум несколько логических ошибок.
  Все они так или иначе были связаны с репопами зоны.
  Отсюда мораль : активней проверяйте что происходит при репопе, используйте команду
  ZReset для тестирования этого момента.


  *****************************************************************************
  ** В ответ сова нападает.

  ** можно было бы oforce mob_27208 уб %actor.name% 
  ** Но хотелось, чтобы сова была недоступна для убивания до того
  ** как возмут яйцо.
  ** Следовательно надо создать ее при первом подымании яйца

  ** Проверка что это первое поднятие предмета и на всякий случай что в нужной комнате
if ( (%actor.room%==27223) && (%self.is_var_exist("var_27200_%self.id%")%==0) )
 
  ** Предметов много - имена должны быть уникальны отсюда суффикс _%self.id%
  ** id уникален лишь в пределах одной загрузки сервера, поэтому яйца !rent
  %self.create_var("var_27200_%self.id%")%  
  
  ** загружаем монстра (сову)
  oload mob 27208
  
  ** Выдаем сообщения что монстр прибыл
  oecho Где-то вверху затрещали ветки и у Вас над головой пролетело что-то большое и страшное.
  oechoaround %actor% Потревоженная вмешательством сова с криком набросилась на %actor.rname%! 
  osend %actor% Потревоженная вмешательством сова с криком набросилась на Вас!

  ** Заставляем сову атаковать игрока
  oforce mob_27208 уб %actor.name% 
end
~
#27201 1
При встрече с шакалом он атакует игрока с 50% вероятностью.~
0 g 50
~


 ** Если поставить wait 1 - он будет атаковать только первого зашедшего в клетку
 ** В данном варианте он пробует атаковать каждого входящего

 ** Если в комнату вошел не игрок (а монстр)
 ** Прекратить выполнение триггера
 ** Иначе шакалы будут атаковать других монстров.
 
if ( %actor.vnum% != -1 )
  halt
end

 
kill %actor.name% 
~
#27202 1
Когда игрок берет бутылку его атакует змея (если есть)~
1 g 100
~

  ** Проверка комнаты и наличия монстра в ней 
if ( (%actor.room%==27206) && %exist.mob(27205)% )

  ** Выдача сообщения
  oecho Внезапно что-то соскользнуло с темного стекла бутылки.

  ** Заставляет змею атаковать
  oforce mob_27205 уб %actor.name% 
end
~
#27203 1
Встреча с бароном~
0 g 100
~
 ** Барон при первой встрече за репоп инициализирует переменную.
 ** Если квест еще не взят - барон предлагает взять его. 
 ** Если квест 1 или 2  взят входящим - он напоминает

 ** Если в комнату вошел не игрок (а монстр)
 ** Прекратить выполнение триггера
if ( %actor.vnum% != -1 )
  halt
end


 ** Если переменной еще нет (первый раз за репоп пришли к барону)
if ( %self.is_var_exist("var_27203")%==0 )

  ** Создать переменную var_27203 прицепленную к барону
  %self.create_var("var_27203")%

  ** Установить удаление этой переменной во время ресета зоны (репопа)
  %self.set_var_reset("var_27203" "reset_zone" "272")%

  ** Установить значение переменной в first_quest
  %self.set_var("var_27203" "first_quest")%
end

  ** Если первый квест доступен... (переменная = first_quest)
if ( %self.get_var("var_27203")%==first_quest )
 
  ** Ждать пока все войдут в комнату (если входит группа)
  wait 1

  ** Речь барона
  say Надо же, кто-то решил почтить меня своим визитом.
  stand
  say Ну ладно. Что явилось целью Вашего прихода? 
  say Если пришли просить милостыню, то убирайтесь отсюда подальше.
  say Если ищете работу, то так и скажите.
end


  ** Если первый квест выдан но не закончен
if ( ( %self.is_var_exist("var_27203")% == 1 ) && ( %self.get_var("var_27203")%==first_quest_given  ) )

  ** Первый квест выдан входящему и еще не выдан второй
  if  ( %actor.is_var_exist("var_27204")% == 1 ) &&  ( %actor.get_var("var_27204")% != second_quest_take )
    
    ** Подождать пока все игроки группы войдут
    wait 1

    ** Выразить свою мысль что пора Ж)
    say Ну и где-ж моя бутылка ?
  end
end


  ** Если второй квест выдан но не закончен
if ( ( %self.is_var_exist("var_27203")% == 1 ) && ( %self.get_var("var_27203")%==second_quest_given  ) )

  ** Второй квест выдан входящему 
  if  ( ( %actor.is_var_exist("var_27204")% == 1 ) && ( %actor.get_var("var_27204")% == second_quest_take ) )
    
    ** Подождать пока все игроки группы войдут
    wait 1

    ** Выразить свою мысль что пора Ж)
    say Ну и как там в лесу ?  Медведь тебе не попадался :) ?
    say Сходи поищи его... он доообрый.
    ** Задействовать социал
    хохо
  end
end
~
#27204 1
Когда игрок говорит у барона ключевую фразу~
0 d 1
ищу работа~
  ** Срабатывает только на фразы со словами "ищу" или "работа"

  ** Если первый квест еще не выдан (иначе выдавать его не надо Ж))
if ( %self.get_var("var_27203")%==first_quest )

  ** Рассказать что надо сделать по первому квесту
  ул %actor.name% 
  say Значит говоришь хочешь поработать?.
  say Тогда для начала принеси мне бутылочку вина из кладовой. 
  say Правда ее охраняет мой верный пес.
  say Сходи к кухарке, может быть она что-нибудь посоветует тебе.

  ** Установить переменную в значение "первый квест выдан"
  %self.set_var("var_27203" "first_quest_given")%

  ** Создать переменную прикрепленную к игроку и показывающую что именно ему дали первый квест
  %actor.create_var("var_27204")%

  ** Сбрасывать эту переменную при репопе этой зоны (зоны 272)
  ** При уходе в ренту переменная тоже сбросится... а вот при смерти нет
  %actor.set_var_reset("var_27204" "reset_zone" "272")%

  ** Установить значение переменнной
  %actor.set_var("var_27204" "first_quest_take")%
end
~
#27205 1
Приветствие кухарки~
0 g 100
~
 ** Если в комнату вошел не игрок (а монстр)
 ** Прекратить выполнение триггера
if ( %actor.vnum% != -1 )
  halt
end


  ** Если вошеднший игрок тот, кто взял 1й квест барона и вещь еще не выдана
if ( ( %actor.is_var_exist("var_27204")%==1 ) && ( %actor.get_var("var_27204")%==first_quest_take ) )

  ** Подождать пока зайдет вся группа
  wait 1

  ** 
  say Ты, %actor.name%, наверное ищешь работу у старика барона.
  say И, наверное, он послал тебя за вином.
  say Совсем скоро сопьется.
  say Ну да ладно, нам-то что с того.
  wait 1s
  say Только вот вход в кладовую охраняет пес.
  say Эта чертова собака ни за что не сдвинется со своего места.
  say Целыми днями лежит у двери и спит.
  say Хотя есть выход. Дай ему этот кусок мяса и иди в кладовую, пока он ест.

  ** Загрузить мясо
  mload obj 27209
  ** Дать мясо. Нельзя давать все, так как у кухарки еще и магазин.
  ** а это значит что у нее в инвентаре продаваемые предметы
  give obj_27209 %actor.name%

  ** Установить игроку переменную в состояние "вещь выдана"
  %actor.set_var("var_27204" "first_quest_get_item")%
end
~
#27206 1
барону дали вина или шкуру~
0 j 100
~
  ** Следует понимать что в общем случае квест может быть засчитан и не тому, кто его
  ** брал, тут все в ваших руках. Конкретно эти квесты реализованы так, чтобы их
  ** засчитывали за выполненные только в том случае, если вещи дает тот, кто их выполнил. 
 
  ** Потенциально можно проверять и тот факт, кто убил медведя, но в данном случае я 
  ** не вижу в этом смысла. Проверку сделать просто: помощью death-триггера на медведе 
  ** устанавливать убийце переменную.
   

  ** Если дали вино
if ( %object.vnum% == 27205 )  

  ** Проверить что бутылка полная 
  if ( %object.val1%!=%object.val0% )
    ** Если бутылка НЕ полная - не брать ее
    return 0

    ** Сказать речь о вреде пьянства Ж)  
    ** !!! Внимание !!!  Во второй фразе учтен пол (если он у игрока женский - все ок будет).
    say Да, это моя заветная бутылочка.   
    say Но она открыта... и мягко говоря не полна... ты ее без меня выжрал%actor.y%...
    say Совсем народ ум потерял... ну да недешево это тебе встанет 

    ** Выдать всем сообщение
    mecho Барон начал покачиваясь подходить к Вам - да, он точно решил вас убить...

    ** Подождать 3 секунды
    wait 3s

    ** Убить игрока, если он не ушел
    kill %actor.name%

    ** Окончить выполнение триггера
    halt 
  end

  ** Если кто-то отравил вино (заклинанием яд к примеру)
  if ( %object.val3%!=0 )
    ** Не брать его
    return 0
    
    ** Сказать речь... гневную
    ** Учесть пол игрока. 
    say Да ты мне ЯДУ принес%actor.q% !!!! 
    say Я это так просто не оставлю.
   
    ** Скастовать на давшего яд.
    ** (кастует не моб, ему спелл знать не обязательно)
    dg_cast 'яд' %actor.name%

    ** Атаковать принесшего ядовитое вино
    kill %actor.name%

    halt 
  end

  ** Все что могло быть с бутылкой не так проверено.
  ** Значит бутылка правильная.

  ** Подождать пока бутылка окажется у барона
  wait 1

  ** Очистить инвентарь барона (там все равно только бутылка может быть)
  mjunk all

  ** Выдать сообщение о том куда он ее дел Ж)
  mecho Барон одним залпом выпил половину бутылки вина и отбросил ее в сторону.
  
  ** Проверить что игрок добыл бутылку дав псу мяса
  if  ( ( %actor.is_var_exist("var_27204")%==1 ) && ( %actor.get_var("var_27204")%==first_quest_dog_happy ) )
    ** Засчитать квест и выдать следующий
    say За твою службу я позволю тебе немного поохотиться в моем лесу.
    say В восточной части двора ты найдешь ворота, за которыми вход в лес.
    say И будь осторожнее. Я давно не был в лесу и скорее всего там завелись дикие звери.
    say И еще... Ты наверное видел%actor.y% маленького медвежонка во дворе?
    say Он пришел из леса несколько месяцев назад. Я подозреваю, что в лесу есть медведи и покрупнее.
    say Если ты принесешь мне шкуру одного из них для коллекции, я награжу тебя.

    ** Отпереть дверь
    mdoor 27205 восток flags ab
    mdoor 27213 запад  flags ab

    ** Установить игроку флаг - ему выдан квест два.
    %actor.set_var("var_27204" "second_quest_take")%

    ** Установить барону флаг что он выдал второй квест
    %self.set_var("var_27203" "second_quest_given")%  

  else
    ** Если игрок добыл бутылку не дав псу мяса или ему не был выдан квест

    ** Тут следует сказать что пес принимая мясо от любого засчитывает это действие
    ** тому, кто получил квест. Возможно это не вполне логично, но логичнее случая,
    ** когда пес принимал бы мясо только от того, кому выдан квест Ж)
    ** В любом случае нас интересует был ли пес "задобрен" хоть кем-то... или он просто был убит.
    ** Можно было бы проверить жив ли пес, 

    ** Вариант первый - вино дает не тот кому поручено
    if ( %actor.is_var_exist("var_27204")%==0 )
      say Да ты никак убил%actor.y% одного из моих слуг ?!?!?!?
      say Или моего ПСА ?!!?!?!?!
      say И еще являешься ко мне, и угощаешь меня вином, которое украл%actor.y% у меня же ?!?!?!
      say Это тебе так просто не пройдет !!!!!!
      mecho Барон начал покачиваясь подходить к Вам - да, он точно решил вас убить...
      wait 3s
      kill %actor.name%
    else
      ** Вариант второй - квест поручен, вино взято, но вот псу мяса не дали
      ** значит убили его болезного...
 
      say Я вижу тебя насквозь : небось пса моего убил%actor.y% ?!?
      say Да вон и руки в крови у тебя... 
      say Ну все - держись... щас я тебя убивать буду.
      хохо
      wait 3s
      kill %actor.name%
    end
  end

  halt
end

  ** Если вещь - шкура медведя
if (%object.vnum% == 27210)
  ** Подождать пока дадут и удалить шкуру.
  wait 1
  mjunk all

  ** Если шкуру дал тот, кому поручено
  if ( %actor.get_var("var_27204")%==second_quest_take )
    say О.. замечательная шкурка... правда, в некоторых местах попорчена, но это мелочи.
    mecho Барон на секунду вышел из кабинета и тут же вернулся.
    say Я отпер дверь в каморку, где я храню свои трофеи.
    say Поищи там, может найдешь себе что-нибудь.
    say Только вот что еще... я ключ от сундука куда-то дел...
    say Может в лесу где потерял, а может в каморке завалился куда.
    say Ну да это теперь твоя проблема - получить свою награду.

    ** Открыть дверь в кладовку
    ** Фактически барон никуда не выходит, а дистанционно открывает дверь
    ** для любителей громких эффектов можно послать сообщения во все комнаты, через
    ** которые проходит барон. (Барон пришел оттуда - Барон ушел туда)
    ** для этого используют две команды типа mat и mecho
    ** Самого барона лучше НЕ ТРОГАТЬ, иначе его по пути можно убить/обворовать итп
    mdoor 27208 восток flags ab
    mdoor 27212 запад  flags ab
  else
    ** Если шкуру дал не тот кому поручен квест, а вообще левый человек.
    say Так... ты принес%actor.q% шкуру. Это хорошо.
    say Но одного не могу понять... откуда ты узнал%actor.y% что она мне нужна.
    дум
    say Да ты наверное замочил%actor.y% там в лесу одного из моих слуг, да еще и пытал%actor.y% его, 
    say чтобы вызнать зачем я его туда послал ?
    say Зря ты это сделал%actor.y%... очень зря.
    kill %actor.name%
  end

  ** Установить барону флаг что он второй квест окончен
  %self.set_var("var_27203" "second_quest_end")%  

  halt
end


  ** Если предмет какой-нибудь другой - не брать его
return 0
say Что за ерунду ты мне принес%actor.q% ?
say Убирайся отсюда поскорее, пока я не избил тебя до смерти!
~
#27207 1
Псу дали мясо ~
0 j 100
~
  ** Пес получая мясо выгружается и загружает другого пса
  ** по виду они одинаковы, но второй не загораживает проход на запад.
  ** Если бы была возможность то-же можно было бы путем изменения флага 
  ** на псе достичь.

  ** Имейте в виду, что в zon-файле необходимо выгрузить второго пса
  ** и опять загрузить первого... чтобы после репопа все было нормально

  ** Если объект это мясо
if (%object.vnum% == 27209)
  ** Подождать пока он окажется у пса, выгрузить его
  ** и выдать сообщения.
  ** Возможно нелишним было бы проверить мясо на отравленность
  wait 1
  mjunk all
  mecho Пес одним махом проглотил кусок мяса.
  mecho Кажется отношение этого лохматого создания к Вам несколько изменилось.

  ** Установить персонажу выполняющему квест переменную.
  ** Теперь у барона будет ясно что он подкупил пса мясом, 
  ** а не убил его (к примеру)
  %actor.set_var("var_27204" "first_quest_dog_happy")%

  ** Загрузить "другого пса"
  mload mob 27217

  ** Выгрузить пса загораживающего дорогу.
  ** !!ВНИМАНИЕ!! после выгрузки триггер прервется, так как выгрузится
  ** вместе с псом, поэтому эта строка написана в самый-самый конец
  ** после нее ничего уже не будет выполняться
  mpurge mob_27204
else
  ** если это не мясо - не брать его
  ** возможно следовало написать триггер так, чтобы
  ** пес ел любую еду, но при этом не пропускал
  ** а про нужное мясо рассказать у кухарки, что оно специальное.
  ** Это мелкое улучшение останется на совести билдер и моей Ж)
  ** Ядовитое мясо тут тоже не учитывается... даже моему терпению
  ** есть предел Ж) 

  return 0
  mecho Пес лишь безразлично посмотрел на Вас.
end
~
#27208 1
Медведь умер~
0 f 100
~
  ** Когда медведь помер в его тело загружается шкура
  ** Ключевые предметы (квест-вещи, ключи итп) лучше грузить
  ** именно так, тогда есть 100% вероятность что они будут в теле

  ** Если загрузить предмет в zon-файле, то он может 
  **  1.развалиться от флага ZONEDECAY
  **  2.развалиться от повреждений (как доспехи на игроках)    
  **  3.его может кто-нибудь украсть
  **  итп

  ** Тут же можно было бы запомнить в игрока что это он убил медведя.
  ** (см. триггер барона)   
  ** Но это было признано излишним.

mload obj 27210
~
#27210 1
Встреча с друидом~
0 g 100
~
  ** Встреча с друидом.
  ** Друид предлагает загадать загадку
  ** Если игрок соглашается он дает ему для начала задание.
  ** Задание дается любому количеству игроков, результат принимается только у первого.
  ** Задание надо успеть за 5 минут выполнить иначе больше ТЕБЕ заданий в этот репоп не дадут.
  ** Принесшему цветок друид загадывает одну из нескольких загадок 
  ** если персонаж отгадывает ему добавляется немного опыта + дается ингридиент + пояс если они еще есть 
  ** и свезло

 ** Если в комнату вошел не игрок (а монстр)
 ** Прекратить выполнение триггера
if ( %actor.vnum% != -1 )
  halt
end


  ** Аналогично барону (см 27203)
if ( %self.is_var_exist("var_27210")%==0 )
  %self.create_var("var_27210")%
  %self.set_var_reset("var_27210" "reset_zone" "272")%
  %self.set_var("var_27210" "quest_available")%
end

if ( %self.get_var("var_27210")%==quest_available )
  wait 1
  say Здравствуй, %actor.name%!
  say Я вижу искру разума, что теплится в твоих глазах.
  say Согласишся ли ты разгадать одну из моих загадок?
  say Если ответишь правильно - я награжу тебя.
  say А может мы уже встречались ? И ты сделал то, что я велел ?
end
~
#27211 1
Отказ от загадки загаданой друидом~
0 d 1
нет~

  ** Если игрок говорит нет, и еще никто не согласился
if ( ( %self.is_var_exist("var_27210")%==1 ) && (%self.get_var("var_27210")% == quest_available) )
  say Ну ладно, ступай тогда своей дорогой и оставь меня наедине с духами леса.
end
~
#27212 1
Согласие на загадку - посыл за цветком~
0 d 1
да~

  ** Если игрок сказал да и еще никто не выполнил квест с цветком

if ( ( %self.is_var_exist("var_27210")%==1 ) && ( %self.get_var("var_27210")% == quest_available ) )

  ** Задание дается любому количеству игроков, результат принимается только у первого.
  ** Задание надо успеть за 5 минут выполнить иначе больше ТЕБЕ заданий не дадут.

  ** генерация имени для переменной
  set varname1 var_27212_1_%actor.id%
  set varname2 var_27212_2_%actor.id%
  
  ** если такой переменной нет - дать квест, иначе не давать
  if ( %self.is_var_exist("%varname2%")% == 0 ) 

    say Хихи
    mecho Не так быстро.  
    mecho Для того чтобы получить загадку тебе придется доказать свою разумность.
    mecho Четко следуй моим инструкциям.
    mecho Возми этот специальный ножик, он не простой, он магический. 
  
    mload obj 27217
    дать все %actor.name%
    
    mecho Возми ножик в руки. Пойди в лес. Ищи в лесу желтый цветок.
    mecho Переворачивай коряги, поднимай камни. Делай это пока не найдешь.
    mecho Имей в виду, ты ищещь не простой цветок, а магический, он может быть где угодно. 
    mecho Разглядеть его непросто, но возможно. Если перевернешь все коряги и не найдешь его,
    mecho подожди и попробуй перевернуть их вновь.
    mecho Когда найдешь цветок не срывай его, просто попытайся взять его, ножик все сделает сам. 
    mecho И поторопись, у тебя всего 5 минут.    


    ** Создать две переменых
    ** Одну для того чтобы контролировать не кончилось ли время.
    ** Схема действия проста:
    **  Мы анализируем наличие этой переменной.    
    **  Если переменная есть - цветок принимается? Если нет - НЕ принимается.
   
    ** Важно удалять эту переменную и после репопа. 
    ** Это необходимо для того чтобы:
    **   1. Игрок мог получить квест в следующий репоп
    **   2. Память сервера не засорялась переменными
    ** 
    ** Далее, почему к self а не к actor. Все просто, если присоединять к игроку,
    ** то уйдя в ренту он сможет скинуть переменную и прийти снова в этот репоп.
    ** Это неверно. 
    ** 
    ** 120 раундов * 2.5 секунд это и есть те самые 5 минут, которые выделяются на задание 
    ** 
    ** Создать переменную и установить ей два ресета.    
    %self.create_var("%varname1%")%
    %self.set_var("%varname1%" "druid_quest_taked")%
    %self.set_var_reset("%varname1%" "reset_zone" "272")%
    %self.set_var_reset("%varname1%" "round" "120")%

    ** Вторая переменная контролирует взял ли уже игрок квест.
    ** Если переменная есть, то квест больше не дается
    ** Сделано для того, чтобы игрок не мог взять второй за репоп квест, после того 
    ** как провалит первый.
    %self.create_var("%varname2%")%
    %self.set_var("%varname2%" "druid_quest_taked")%
    %self.set_var_reset("%varname2%" "reset_zone" "272")%

    ** Значения этим двум переменным выдаются "на всякий случай"
    ** Лучше иметь значение чем не иметь его. (могут быть проблемы с удалением пустых переменных)
    ** (как показывает практика эти значения еще и используются в дальнейшем Ж))

    ** Оставлено под расширение пока не используется
    ** Вот например ИМЯ не успел и был послан мною далеко-далеко.
    ** И еще, ИМЯ неверно следовал моим инструкциям и был послан мною в да-альний поход.
  else
    say Что "ДА!", я тебе уже все сказал - уходи пока я не разозлился.
  end
end
~
#27213 1
Игрок сказал ответ на загадку~
0 d 1
*~
 ** Если загадка загадана 

if (%self.get_var("var_27210")%==puzzle_gived)

  ** Если загадана НЕ ответившему игроку
  if ((%self.is_var_exist("var_27219_2")%==1) && (%self.get_var("var_27219_2")%!=%actor.id%))
    say %actor.name%, но ведь это НЕ ТЕБЕ я загадывал загадку !
    msend %actor.name% Вы почуствовали себя неважно.
    
    ** Повредить игрока на 10 хитов  
    mdamage %actor.name% 10
    halt
  end

  ** Если сказан правильный ответ
  ** (он запоминается в эту переменную в предыдущем триггере)
  if  (%speech%==%self.get_var("var_27219")%)
    say Молодец!
    say Получай награду.

    ** Использовать временную переменную dg-script
    ** После окончания скрипта она пропадет
    ** Удобно для подобных случаев
    ** Опыта дается в зависимости от уровня.
    ** Важно чтобы на первом уровне игрок не получил 
    ** излишне много опыта.
    eval exp %actor.level% * 300

    ** Выдать опыт. Используйте эту функцию несмотря на то, что она deprecated
    ** и вызывает сообщение о том, что ее лучше заменить на другую
    mexp %actor% %exp%

    ** Еще один блок типа switch
    ** Выбор и загрузка одной из трех вещей
    switch  %random.5% 
    case 1
      mload obj 27212
    break
    case 2
      mload obj 27213
    break
    case 3
      mload obj 27214
    break
    case 4
      mload obj 27215
    break
    default
      mload obj 27216
    done

    ** Загрузить плащ если он не замаксился.
    ** ОЧЕНЬ важно проверить максфактор, иначе эти накидки будут
    ** размножаться неконтролируемо.
    ** Всегда пишите подобные триггеры так, чтобы было ясно сколько процентов Ж)
    if ( %random.100% < 15 ) && (%world.curobjs(27211)% <= 5)
      mload obj 27211
    end
 
    ** дать все игроку 
    give all %actor.name%

    say Да, еще у меня тут какой-то ключ завалялся...
    say Может тебе на что сгодится ?
    mload obj 27219
    give all %actor.name%
    
  else
    ** Если игрок ответил неверно - ему ничего и не дадут.
    say Неверно!
    say Так что извини, с наградой придется подождать.
  end

  ** очистить и заново создать выходы
  mecho Корни разошлись вновь образуя проход.
  mdoor 27239 восток purge
  mdoor 27238 запад  purge
  mdoor 27239 восток room 27238
  mdoor 27238 запад  room 27239

  ** проставить значение переменной = квест окончен
  %self.set_var("var_27210" "puzzle_finished")%

  halt
end

if ( (%self.get_var("var_27210")%==puzzle_timeout) && (%self.is_var_exist("var_27219_2")%==1) && (%self.get_var("var_27219_2")%!=%actor.id%) )
  say Извини, но уже поздно.
end
~
#27214 1
Дикобраз стреляет иголками~
0 k 50
~
  ** Дикобраз в бою повреждает ВСЕХ игроков скидывая иголки
  ** Он делает это с вероятностью 50%, но не чаще каждого третьего раунда
  ** Реализуется через переменную
  ** Дикобраз только может быть и не один, и мы используем переменную со динамическим именем.

  ** Вычисляем имя переменной
set varname var_27214_%self.id%

  ** mecho %varname%
  
 
  ** Если переменной нет, то пора снова выбрасывать иглы Ж)
if ( %self.is_var_exist("%varname%")% == 0 )
  ** Создать переменную
  %self.create_var("%varname%")%

  ** Установить удаление этой переменной через 3 раунда
  %self.set_var_reset("%varname%" "round" "3")%


  ** Выдать сообщение
  mecho Дикобраз сделал резкое движение, и во все стороны разлетелись его иглы.

  ** Взять первого игрока/моба в комнате 
  ** set char %people.%self.room%%
  calcuid where %self.room% room
  set char %where.people%


  ** Пока остается хоть один игрок (char устанавливается далее)
  while %char%

    ** Взять игрока 
    set pc %char.next_in_room%

    ** Проверить что это игрок а не моб
    if %char.vnum% == -1
      ** рандомный дамадж на 1-20
      mdamage %char.name% %random.20%
    end

    ** Если игрок не последний 
    if %pc%
       ** Взять следующего
       makeuid char %pc.id%
    else
       ** Поставить флаг 0 для прекращения цикла
       set char 0
    end
  done
end
~
#27215 1
Взятие цветка~
1 g 100
~
  ** Если цветок берут взяв в руки специальный предмет (ножик)
  ** После того как цветок принесен друиду новые цветки перестают грузиться

  ** Потенциальная сложность в том, что вначале можно набрать заданий и нарвать цветов,
  ** а потом отнести их все к друиду, тогда будет принят только один, что не очень хорошо.
  ** С другой стороны квест должен быть засчитан только один раз.
  ** Если прекращать загружать цветки после первого срезанного, то хорошим это 
  ** не кончится, так как если кто-то срежет цветок и не исполнит квест, то
  ** 
  ** Обратите внимание, что если бросать и подбирать цветок, то ничего
  ** происходить не будет. Так как переменная будет создана при первом его подъеме.

  set objvarname var_27215_%self.id%
  
  if ( %self.is_var_exist("%objvarname%")%==0 )
    %self.create_var("%objvarname%")%

    ** проверить что магический ножик или в правой руке или в левой или в обеих
    if (( %actor.is_weared(27217)%==16 ) || ( %actor.is_weared(27217)%==17 ) || ( %actor.is_weared(27217)%==18 ))
      oechoaround %actor.name% %actor.name% наклонил%actor.u% и аккуратно срезал%actor.y% цветок.
      osend %actor.name% Вы наклонились и аккуратно срезали цветок.

      %self.set_var("%objvarname%" "good_flower")%
    else
      oechoaround %actor.name% %actor.name% наклонил%actor.u% и безжалостно вырвал%actor.y% цветок из земли.
      osend %actor.name% Вы наклонились и безжалостно вырвали цветок из земли.

      %self.set_var("%objvarname%" "bad_flower")%
    end
  end 




~
#27216 1
Перевернуть корягу~
1 c 100
перевернуть~
  ** Переворачивание коряги.

  ** В идеале проверить что игрок набрал "перевернуть корягу", а не "перевернуть"
  ** Для этого надо проверять аргумент. (см. ниже)
  ** Однако в зонах для новичков иногда целесообразно упростить им задачу
  ** не проверяя аргумент. Особенно это актуально когда в аргументе встречается
  ** буква я. Иначе те, у кого она глючит не смогут использовать этот триггер

  ** Всего в игре 5 коряг и один камень, используя каждый из объектов
  ** Игрок получает цветок или нет. Щанс равен количеству уже 
  ** использованных объектов * 20%. То есть повернув 5 коряг
  ** игрок гарантированно найдет камень.
  
  **  Есть шанс (зависящий от интеллекта персонажа)
  ** что персонаж очень сильно испугается и убежит в 
  ** произвольную комнату леса (из некоторого набора)
  **  Собственно это пример того, как надо реализовывать
  ** ручку при нажатии на которую кого-нибудь куда-нибудь
  ** телепортирует.

  ** проверить аргумент - еслиб не буква я - можно было бы оставить  
  ** if ( (%arg%!=корягу) && (%arg%!=коряга) )    
  **   halt
  ** end
 
  ** Проверить интеллект и если он мал - кинуть рандомчик 
  ** Если рандом выпал - телепортировать убежавшего игрока
  if ( %actor.int% < 15 )
     if ( %random.100% < 50 )

       ** Выбираем комнату            
       switch  %random.23% 
         case 1 
           set roomnumber 27215
         break
         case 2 
           set roomnumber 27216
         break
         case 3 
           set roomnumber 27217
         break
         case 4 
           set roomnumber 27218
         break
         case 5 
           set roomnumber 27219
         break
         case 6 
           set roomnumber 27220
         break
         case 7 
           set roomnumber 27221
         break
         case 8 
           set roomnumber 27222
         break
         case 9 
           set roomnumber 27223
         break
         case 10 
           set roomnumber 27224
         break
         **
         **
         **
         case 11 
           set roomnumber 27226
         break
         case 12 
           set roomnumber 27227
         break
         case 13 
           set roomnumber 27228
         break
         case 14 
           set roomnumber 27229
         break
         case 15 
           set roomnumber 27230
         break
         case 16 
           set roomnumber 27231
         break
         case 17 
           set roomnumber 27232
         break
         case 18 
           set roomnumber 27233
         break
         case 19 
           set roomnumber 27234
         break
         case 20 
           set roomnumber 27235
         break
         case 21 
           set roomnumber 27236
         break
         case 22 
           set roomnumber 27237
         break
       default
         set roomnumber 27238
       done 

       ** Выдать сообщение окружающим игрока
       oechoaround %actor.name% %actor.name% перевернул%actor.y% корягу, посмотрел%actor.y% под нее и внезапно
       oechoaround %actor.name% с криком ужаса убежал%actor.y% в лес. Чего это он%actor.y% ? 
       oechoaround %actor.name% Наверное он%actor.y% испугал%actor.u% этой маленькой черной лягушки, которая сидит
       oechoaround %actor.name% под корягой. Был%actor.y% бы умнее - знал%ator.y% бы, что нечего бояться.
       
       ** Выдать сообщение игроку
       osend %actor.name% Вы перевернул корягу, и посмотрели под нее.
       osend %actor.name% В первый момент Вам показалось, что под корягой ничего нет.
       osend %actor.name% Однако присмотревшись вы увидели под ней что-то маленькое, черное и страшное.
       osend %actor.name% Вспомнив бабушкины сказки вы поняли, что это "ужасное зло живущее под корягой".
       osend %actor.name% Вам показалось что "зло" выползает оттуда.
       osend %actor.name% Вспомнив бабушкины сказки вы поняли, что щас вам будет плохо-плохо.
       osend %actor.name% Не дожидаясь конца этого ужасного процесса Вы с криком ужаса убежали
       osend %actor.name% в лес... подальше... подальше от этого ужаса.
       osend %actor.name% УХ! Вроде удалось убежать...
       
       ** Выдать сообщение в комнате, куда игрок попал
       oat %roomnumber% oecho Внезапно из леса показал%actor.u% %actor.name% и устало присел%actor.y% на землю.

       oteleport %actor.name% %roomnumber%
 
       ** Прервать выполнение триггера 
       halt 
     end        
  end



  set varname var_27216_%actor.id%

  ** создается переменная с уникальным именем в игроке
  ** если его понесет в ренту - сам виноват
  ** в переменной хранится количество перевернутых коряг

  if ( %actor.is_var_exist("%varname%")%==0 ) 
    %actor.create_var("%varname%")%
    %actor.set_var_reset("%varname%" "reset_zone" "272")%
    %actor.set_var("%varname%" "1")%
  end 
      
     
  eval percent  %actor.get_var("%varname%")%*20
  eval newvalue %actor.get_var("%varname%")%+1
  %actor.set_var("%varname%" "%newvalue%")%

  oechoaround %actor.name% %actor.name% перевернул%actor.y% корягу, и посмотрел%actor.y% под нее.
  osend %actor.name% Вы перевернули корягу, и посмотрели под нее.

  ** если свезло
  if ( %random.100% < %percent% ) 
    osend %actor.name% Да тут оказывается цветок растет !

    ** бросить рандом и прорастить цветок
    switch  %random.4% 
    case 1
      oload obj 27222
    break
    case 2
      oload obj 27221
    break
    default
      oload obj 27220
    done

  else
    osend %actor.name% Обыкновенная коряга... крути не крути она не изменится.    
  end

  ** одну и ту же корягу можно перевернуть раз в 60 секунд...
  ** в принципе можно было бы в каждой коряге запомнить поворачивал ли ее игрок,
  ** но это было признано излишним. Пока прождешь - квест кончится может Ж).
  wait 60 s

~
#27217 1
Поднять камень~
2 c 100
поднять~
  ** См описание триггера с корягой (предыдущего)

  ** Надо понимать что этот триггер сработает только в комнате к которой он присоединен
  ** (таких комнат может быть несколько), в то время как триггер с корягой выполняется
  ** когда в комнате лежит предмет, к которому присоединен триггер.
  ** Какой из способов выбрать дело ваше, но в зависимости от цели бывает предпочтительнее
  ** выбрать один из них

  ** если сила больше 15 - камень поднимается, если меньше - нет
  if ( %actor.str% > 15 )
    wsend %actor.name% Вы ловко подняли камень и переставили его на другое место.
    wechoaround %actor.name% %actor.name% ловко поднял%actor.y% камень и переставил%actor.y% его на другое место.
    wecho На старом месте образовалось еще одно "примятое" место.

    set varname var_27216_%actor.id%

    if ( %actor.is_var_exist("%varname%")%==0 ) 
      %actor.create_var("%varname%")%
      %actor.set_var_reset("%varname%" "reset_zone" "272")%
      %actor.set_var("%varname%" "1")%
    end     
     
    eval percent  %actor.get_var("%varname%")%*20    
    eval newvalue %actor.get_var("%varname%")%+1
    %actor.set_var("%varname%" "%newvalue%")%


    ** если свезло
    if ( %random.100% < %percent% ) 
      wecho Присмотревшись внимательнее, Вы заметили, что на этом месте пророс цветок.
  
      ** бросить рандом и прорастить цветок
      switch  %random.4% 
      case 1
        wload obj 27222
      break
      case 2
        wload obj 27221
      break
      default
        wload obj 27220
      done
    else
    end

    wait 60 s
  else
    wecho Поднатужились, но поднять камень так и не удалось.
  end
~
#27218 1
Рабочий зовет на помощь~
0 k 20
~
  ** Рабочий зовет на помощь.
  ** Зовет во время боя с 20% вероятностью
  ** Рабочих несколько, но здоровый детина помогает только первому позвавшему его
  ** В этом триггере есть большая проблема неочивидная на первый взгляд
  ** Дело в том, к чему прикрепить переменную.
  ** К рабочему - убъют его и переменная удалится
  ** К детине - та же история
  ** К игроку - в ренту уйдет
  ** Единственный выход - это прекрепить ее к комнате.
  ** Не важно какой - все равно переменная удалится в репоп, важно что комната будет всегда Ж).
  ** Главное не забыть удалить детину во время следующего репопа.
   

кричать Кто-нибудь помогите, %actor.name% напал%actor.y% на меня.


if ( %self.is_var_exist("var_27218")%==0 ) 
  ** получить переменную типа комната
  calcuid where %self.room% room
  
  ** Создать переменную у комнаты и поставить ей флаг ресета - удалять в репоп
  %where.create_var("var_27218")%
  %where.set_var("var_27218" "already_called")%
  %where.set_var_reset("var_27218" "reset_zone" "272")%

  mecho Здоровый детина появился откуда-то.
  mload mob 27219
  mforce mob_27219 убить %actor.name%
else
  mecho Лишь молчание было ему ответом.
end

 

~
#27219 1
Друиду дали вещь - если правильную -  загадывание загадки~
0 j 100
~

set objvarname var_27215_%object.id%
set varname1 var_27212_1_%actor.id%
set varname2 var_27212_2_%actor.id%

if (( %self.is_var_exist("%varname2%")%==1 ) && ( %object.vnum% == 27220 ) && ( %object.get_var("%objvarname%")% != good_flower ))
  return 0
  %self.set_var("%varname2%" "druid_quest_failed_flower")%

  say Я тебе говорил ножик в руки взять ?
  say Ведь говорил же... ты не взял%actor.y%, теперь этот цветок только навыброс годится.
  say Прочь с глаз моих долой.

  halt
end


if ( %object.vnum% == 27220 )

  ** не выполнен ли уже квест ?
  if ( ( %self.is_var_exist("var_27210")%==1 ) && ( %self.get_var("var_27210")% == quest_available ) )
    
    ** Положен ли квест персонажу
    ** !!! Внимание !!!
    ** Проверяется 2 переменных. 1 - не поздно ли. 2 - не запорол ли игрок квест принеся испорченный цветок.
    ** Если все хорошо - проходим дальше, если нет - даем сообщение и заканчиваем триггер
    ** (это чтобы многоэтажных if-ов не городить)

    ** Поручен ли квест 
    if ( %self.is_var_exist("%varname2%")%==1 )
      ** время кончилось 
      if ( %self.is_var_exist("%varname1%")%==0 ) 
        say Ты опоздал%actor.y%, в следующий раз будь порасторопнее. 
        return 0
        halt      
      end

      ** квест уже один раз запорот 
      if ( %self.get_var("%varname2%") == druid_quest_failed_flower )
        say Ты уже один принес%actor.y%... этот небось такой-же... дефектный... уйди, не мозоль глаза.
        return 0
        halt      
      end
    else
      say Но я тебе ничего не поручал.
      return 0
      halt      
    end


      ** Если все хорошо

      wait 1
      mjunk all

      ** Установить в переменную - загадка дана
      %self.set_var("var_27210" "puzzle_gived")%

      ** Создать еще одну переменную - в нее будет записан верный ответ
      %self.create_var("var_27219")%
      %self.set_var_reset("var_27219" "reset_zone" "272")%

      ** Установить переменную - кому дана загадка 
      ** Запоминается его id - лучше чем имя, так запомнить его имя
      %self.create_var("var_27219_2")%
      %self.set_var("var_27219_2" "%actor.id%")%
      %self.set_var_reset("var_27219_2" "reset_zone" "272")%

      ** Сказать пару фраз и закрыто дверь на восток
      ** Теперь игрок в месте откуда нет выхода.
      ** Надо что-то ответить... или сидеть ждать Ж) 
      say Молодец! Ты принял%actor.y% мой вызов.
      say Теперь обратного пути нет.
      mecho Корни дерева внезапно сплелись за вашей спиной, преградив путь на восток.
      mecho Вы поняли, что Вам придется дать ответ.
      mdoor 27239 восток flags abcde
      mdoor 27238 запад  flags abcde
    

      say Слушай внимательно:
      say Если ответ в загадке подразумевает число - используй число, а не его
      say написание, например, надо ответить 2, а не "два". 
      say У тебя есть всего 50 секунд на то, чтобы дать ответ.

      ** Пример структуры типа switch
      ** Вначале выбирается случайное число от 1 до 8
      ** Потом в зависимости от того что выпало выполняется один из блоков
 
      switch  %random.8% 
      case 1
        say У кузнеца есть 5 цепей по 3 кольца в каждой.
        say Какое минимальное число колец нужно раскрыть и заново соединить, чтобы соеденить их в 1 цепь?
    
        ** Записать правильный ответ
        %self.set_var("var_27219" "3")%
      break
      case 2
        say У барона семь дочерей. И у каждой из них есть брат.
        say Сколько всего детей у барона?
        %self.set_var("var_27219" "8")%
      break
      case 3
        say Сколько у меня цветов если все они розы кроме двух, все они тюльпаны кроме двух, все они фиалки, кроме двух?
        %self.set_var("var_27219" "3")%
      break
      case 4
        say В комнате четыре угла. В каждом углу сидит кошка. 
        say Напротив каждой кошки по три кошки. На хвосте каждой кошки по одной кошке. 
        say Сколько же всего кошек в комнате?
        %self.set_var("var_27219" "4")%
      break
      case 5
        say В комнате горело семь свечей. 
        say Проходил мимо человек, потушил две свечи. 
        say Сколько свечей осталось?
        %self.set_var("var_27219" "7")%
      break
      case 6
        say Когда я молодо, я душисто на солнце.
        say Когда средних лет, я делаю Вас беспечным.
        say Когда старо, я ценюсь больше чем когда-либо
        say Кто я? 
        %self.set_var("var_27219" "вино")%
      break
      case 7
        say Моя жизнь может быть измерена в часах.
        say Я служу, когда пожираюсь.
        say Когда я тонка я быстра.
        say Когда я толста я медленна.
        say Ветер мой враг
        say Кто я? 
        %self.set_var("var_27219" "свеча")%
      break

      ** В случае если выпало иное чем уже обработано (т.е. не 1, не 2... не 7) 
      default
        say Что не имеет никакого содержания, но все же вы можете это видеть?
        %self.set_var("var_27219" "дыра")%
      done


      ** Создать переменную, когда она уничтожится через 50 секунд (20 раундов по 2,5с)
      ** загадка будет аннулирована а проход будет восстановлен
      ** это будет реализовано в триггере вызываемом при уничтожении
      ** переменной, см следующий триггер
      ** 
      %self.create_var("var_27219_timer")%
      %self.set_var("var_27219_timer" "puzzle_active")%
      %self.set_var_reset("var_27219_timer" "round" "20")%
      %self.set_var_reset("var_27219_timer" "reset_zone" "272")%

      halt
  else
    return 0
    say Спасибо, но мне это уже не нужно.
  end
end


if ( %object.vnum% == 27221 ) || ( %object.vnum% == 27222 )
  say Ну ты супер просто. Принес%actor.q%. Я же говорил Ж-Е-Л-Т-Ы-Й !!!!  
end


return 0
say И зачем мне это ?
~
#27220 1
Переменная на друиде обнулилась~
0 p 100
~
  ** Этот триггер не был написан для первой версии зоны, что оказалось ошибкой
  ** Проблема в поведении друида. 
  ** Если посмотерть в триггере загадывания загадки, то можно увидеть
  **  
  ** say Теперь обратного пути нет.
  ** mecho Корни дерева внезапно сплелись за вашей спиной, преградив путь на восток.
  ** mecho Вы поняли, что Вам придется дать ответ.
  ** mdoor 27239 восток flags abcde
  ** mdoor 27238 запад  flags abcde
  **
  ** То есть выход и вход к друиду убираются.
  ** В то же время в триггере ответа на загадку эти выходы вновь создаются.
  ** 
  ** Остановитесь и подумайте все ли нормально ?
  ** 
  ** А теперь учтите, что флаги комнат НЕ изменяются в момент репопа
  ** единственные комнаты для которых они меняются, это те, для которых
  ** в zon-файле описано закрывание дверей - двери действительно закрываются.
  ** Но у друида нет двери.
  **
  ** Остановитесь и подумайте еще раз : все ли нормально ?
  **
  ** А теперь представьте себе игрока, который взял загадку но потерял связь.
  ** Либо игрока взявшего загадку и сидящего у друида, чтобы уйти от ПК.
  ** На самом деле в обоих случаях произойдет одно и то же
  ** Проход к друиду НИКОГДА не появится пока не перезагрузится игровой сервер.
  ** Особенно опасен второй вариант, ибо первый на самом деле еще можно попробовать
  ** исправить устанавливая определенные флаги в команде zon-файла D
  ** там допустимо задавать битвектор флагов из команды mdoor
  ** задается он в самом конце команды после всех остальных параметров, вручную.
  ** Но если игрок "отсиживается", то репоп не произойдет вообще...
  ** 
  ** Это пример типичной ошибки. Надо учитывать что НЕ ВСЕ изменения скидываются
  ** при репопе. При репопе ВООБЩЕ ничего не скидывается :) при репопе выполняются
  ** команды из zon-файла. Если там нет команды загрузить монстра - он не загрузится,
  ** нет команды закрыть дверь - она не закроется, нет команды... не выполнится.
  ** 
  ** ОЧЕНЬ внимательно думайте над тем, что делаете и какие следы остаются 
  ** от этого после репопа. После репопа зона должна быть готова к приему
  ** новой группы игроков - следите за этим.

  ** В данном случае решение должно быть таким:
  ** загадка выдается на какое-то время (скажем минуту)
  ** Если игрок не отвечает - загадка аннулируется и проход открывается
  ** Таким образом в этой комнате можно будет сидеть не более 1 минуты.

  ** Этот триггер вызывается при очищении переменной и восстанавливает статус-кво
  ** (если это еще не сделано в результате ответа игрока)


  ** Очищается нужная переменная и при ее очищении загадка еще активна
  ** т.е. попытка отгадать загадку еще не сделана
  if  ( %var% == var_27219_timer ) 
  
    ** Показать что загадка уже провалена по времени (если ресет по времени)
    if ( %self.is_var_exist("var_27210")% == 1 ) 
      if ( ( %self.get_var("var_27210")% == puzzle_gived ) && ( %msg% == round ) )
        %self.set_var("var_27210" "puzzle_timeout")%
      end
    end

    say Твое время истекло.
    say В следующий раз думай быстрее.

    ** очистить и заново создать выходы
    mecho Корни разошлись вновь образуя проход.
    mdoor 27239 восток purge
    mdoor 27238 запад  purge
    mdoor 27239 восток room 27238
    mdoor 27238 запад  room 27239
  end


  ** !!! ВНИМАНИЕ !!!
  ** Нижеследующий кусок кода приводит к суровым и фатальным последствиям.
  ** НИКОГДА не удаляйте переменную в триггере, который вызывается при их очистки :).
  ** Этот механизм был заменен на механизм с двумя переменными.
  ** Одна отражает надо ли давать игроку квест или один раз уже дали - удаляется при репопе.
  ** Вторая отражает не просрочен ли квест - удаляется когда пройдет время и квест  
  ** будет просрочен лиюо при репопе. 
  ** Возможно если сделать wait 1 то все нижезакомментаренное заработает, но лучше НЕ ПРОБОВАТЬ.
  ** Вообще delete нужен редко, не злоупотребляйте им
  ** 
  **** Это тоже очень важно.
  **** Удаление переменных для игроков в квесте с цветком 
  **** Заметим что удаляются лишь переменные содержащие var_27212_
  **if (( %msg% == reset_zone ) && ( %var.contains(var_27212_)%==1 ))
  **  %self.delete_var( "%var%" )%
  **end
~
$
$
